apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.gretty'

ext { CBL_VERSION = "3.1.0-413" }

def USE_LOCAL_MAVEN = project.hasProperty("useLocalMaven")
def CBL_LIB_EXT = (!USE_LOCAL_MAVEN) ? "" : "-macos"

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies { classpath 'org.gretty:gretty:4.0.3' }
}

group 'com.couchbase.lite'
version '3.1.0'

java {
    targetCompatibility JavaVersion.VERSION_1_8
    sourceCompatibility JavaVersion.VERSION_1_8
}

repositories {
    if (USE_LOCAL_MAVEN) { mavenLocal() }
    else {
        maven {
            url "https://proget.sc.couchbase.com/maven2/internalmaven/"
            allowInsecureProtocol = true
        }
    }

    mavenCentral()
    google()
}

dependencies {
    compileOnly 'jakarta.servlet:jakarta.servlet-api:4.0.4'
    compileOnly 'javax.activation:activation:1.1.1'

    compileOnly 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.2'

    // Jersey:
    implementation 'org.glassfish.jersey.containers:jersey-container-servlet:3.1.1'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:3.1.1'
    implementation 'org.glassfish.jersey.bundles:jaxrs-ri:3.1.1'

    // Jersey-Media:
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:3.1.1'
    implementation 'org.glassfish.jersey.media:jersey-media-multipart:3.1.1'

    // Couchbase:
    implementation "com.couchbase.lite:couchbase-lite-java-ee${CBL_LIB_EXT}:${CBL_VERSION}"
}

gretty {
    contextPath = '/'
    loggingLevel = 'ALL'
    enableNaming = true
    consoleLogEnabled = true
    fileLogEnabled = false
    contextConfigFile = file('config/META-INF/jetty-env.xml')
}

