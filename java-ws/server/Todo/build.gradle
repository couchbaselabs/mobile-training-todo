apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'

ext {
    COUCHBASE_LITE_VERSION = "3.0.0-beta02"
    TOMCAT_VERSION = '9.0.24'
}

group 'com.couchbase.lite'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.5'
    }
}

repositories {
//    // local maven
//    mavenLocal()
//    // internal maven
//    maven {
//        url "http://proget.build.couchbase.com/maven2/internalmaven"
//        allowInsecureProtocol = true
//    }
    // public maven
    maven { url "https://mobile.maven.couchbase.com/maven2/dev" }

    mavenCentral()
}

dependencies {
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly 'javax.xml.bind:jaxb-api:2.3.1'
    compileOnly 'javax.activation:activation:1.1.1'

    // Jersey:
    implementation 'org.glassfish.jersey.containers:jersey-container-servlet:2.28'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:2.28'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.1'
    implementation 'org.glassfish.jersey.bundles:jaxrs-ri:2.29.1'

    // Jersey-Media:
    implementation 'org.glassfish.jersey.media:jersey-media-multipart:2.29'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:2.29'

    // Couchbase:
    implementation "com.couchbase.lite:couchbase-lite-java-ee:${COUCHBASE_LITE_VERSION}"

    // Tomcat:
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${TOMCAT_VERSION}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:9.0.0.M6",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${TOMCAT_VERSION}"

    testImplementation 'junit:junit:4.13.2'
}

tomcat {
    httpPort = 8080
    httpProtocol = 'org.apache.coyote.http11.Http11Nio2Protocol'
    ajpProtocol  = 'org.apache.coyote.ajp.AjpNio2Protocol'
    contextPath = '/'
}

[tomcatRun, tomcatRunWar].each { task ->
    fileTree("libs").visit { FileVisitDetails details ->
        task.additionalRuntimeResources << file(details.file.path)
    }
    task.configFile = file('config/META-INF/context.xml')
}
